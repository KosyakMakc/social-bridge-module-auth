plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.9'
}

group = 'io.github.kosyakmakc'
version = System.getenv("VERSION") ?: '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "gitea"
        url = "https://git.kosyakmakc.ru/api/packages/kosyakmakc/maven"
    }
}

dependencies {
    implementation project(":")

    compileOnly "io.github.kosyakmakc:SocialBridge:0.5.0"
    testImplementation "io.github.kosyakmakc:SocialBridge:0.5.0"
    compileOnly "io.github.kosyakmakc:SocialBridge-telegram:0.5.0"
    testImplementation "io.github.kosyakmakc:SocialBridge-telegram:0.5.0"

    compileOnly "io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT"
    testImplementation "io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT"
}

runPaper.folia.registerTask()
tasks {
    runFolia {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
        downloadPlugins {
            github("kosyakmakc", "SocialBridge", "0.5.0", "SocialBridge-paper-0.5.0.jar")
            github("kosyakmakc", "SocialBridge-telegram", "0.5.0", "SocialBridge-telegram-paper-0.5.0.jar")
        }
    }
}

shadowJar {
    archiveBaseName.set('AuthSocial-paper')
    archiveVersion.set(version.toString())
    archiveClassifier.set('')
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}